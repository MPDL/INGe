<?xml version="1.0" encoding="UTF-8"?>
<!--
	CDDL HEADER START The contents of this file are subject to the terms
	of the Common Development and Distribution License, Version 1.0 only
	(the "License"). You may not use this file except in compliance with
	the License. You can obtain a copy of the license at
	license/ESCIDOC.LICENSE or http://www.escidoc.org/license. See the
	License for the specific language governing permissions and
	limitations under the License. When distributing Covered Code, include
	this CDDL HEADER in each file and include the License file at
	license/ESCIDOC.LICENSE. If applicable, add the following below this
	CDDL HEADER, with the fields enclosed by brackets "[]" replaced with
	your own identifying information: Portions Copyright [yyyy] [name of
	copyright owner] CDDL HEADER END Copyright 2006-2010
	Fachinformationszentrum Karlsruhe Gesellschaft für
	wissenschaftlich-technische Information mbH and Max-Planck-
	Gesellschaft zur Förderung der Wissenschaft e.V. All rights reserved.
	Use is subject to license terms.
-->
<!--
	Runtime Functions for the transformations from eSciDoc Citation Style Configuration Language 
	to the Citation Style Execution XSLT 
	See http://colab.mpdl.mpg.de/mediawiki/CitationStyleConfigurationConcept
	and https://zim02.gwdg.de/repos/common/trunk/common_services/citationmanager/src/main/resources/Schemas/citation-style.xsd
	Author: Vlad Makarenko (initial creation) $Author$ (last changed)
	$Revision$ $LastChangedDate$
-->
<functions xmlns="http://www.escidoc.de/citationstyle" xmlns:exslt="http://exslt.org/common" xmlns:func="http://www.escidoc.de/citationstyle/functions" xmlns:functx="http://www.functx.com" xmlns:jfunc="java:de.mpg.mpdl.inge.citationmanager.utils.XsltHelper" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
	<!-- RUNTIME FUNCTIONS -->
	<xsl:function name="func:get_year">
		<xsl:param name="date"></xsl:param>
		<xsl:value-of select="substring($date,1,4)"></xsl:value-of>
	</xsl:function>
	<xsl:function name="func:get_month">
		<xsl:param name="date"></xsl:param>
		<xsl:value-of select="substring($date,6,2)"></xsl:value-of>
	</xsl:function>
	<xsl:function name="func:get_month_name">
		<xsl:param name="date"></xsl:param>
		<xsl:variable name="months">
			<m n="0?1">January</m>
			<m n="0?2">February</m>
			<m n="0?3">March</m>
			<m n="0?4">April</m>
			<m n="0?5">May</m>
			<m n="0?6">June</m>
			<m n="0?7">July</m>
			<m n="0?8">August</m>
			<m n="0?9">September</m>
			<m n="10">October</m>
			<m n="11">November</m>
			<m n="12">December</m>
		</xsl:variable>
		<xsl:value-of select="$months/m[matches(tokenize($date,'-')[2],@n)]"></xsl:value-of>
	</xsl:function>
	<xsl:function name="func:get_initials">
		<xsl:param name="str"></xsl:param>
		<xsl:variable name="delim" select="if (contains($str,'-')) then '-' else ' '"></xsl:variable>
		<xsl:variable name="result">
			<xsl:for-each select="tokenize(normalize-space ($str),'\s+|\.\s+|\-\s*')">
				<xsl:value-of select="concat(substring(.,1,1),if (position()!=last()) then concat('.', $delim) else '.')"></xsl:value-of>
			</xsl:for-each>
		</xsl:variable>
		<xsl:value-of select="$result"></xsl:value-of>
	</xsl:function>
	<xsl:function name="func:fname_initials">
		<xsl:param name="fname"></xsl:param>
		<xsl:param name="gname"></xsl:param>
		<xsl:param name="delim"></xsl:param>
		<xsl:value-of select="if (jfunc:isCJK(concat($fname, $gname))) then string-join(($fname,$gname),$delim) else string-join(($fname,func:get_initials($gname)),$delim)"></xsl:value-of>
	</xsl:function>
	<xsl:function name="func:initials_fname">
		<xsl:param name="gname"></xsl:param>
		<xsl:param name="fname"></xsl:param>
		<xsl:param name="delim"></xsl:param>
		<xsl:value-of select="if (jfunc:isCJK(concat($fname, $gname))) then string-join(($fname, $gname),$delim) else string-join((func:get_initials($gname),$fname),$delim)"></xsl:value-of>
	</xsl:function>
	<xsl:function name="func:escapeMarkupTags">
		<xsl:param name="str"></xsl:param>
		<xsl:value-of select="jfunc:escapeMarkupTags($str)"></xsl:value-of>
	</xsl:function>
	<!-- Postprocessing: clean citation -->
	<xsl:function name="func:cleanCitation">
		<xsl:param name="str"></xsl:param>
		<xsl:value-of select="normalize-space(functx:replace-multi($str,('([.,?!:;])\s*(&lt;[/]span&gt;)\s*\1','([.,?!:;])\s*\1','\.&quot;\.','\s+([.,?!:;])','\s*(&lt;[/]?span&gt;)\s*([.,?!:;])','([?!])+\.'),('$1$2','$1','.&quot;','$1','$1$2','$1')))"></xsl:value-of>
		<!-- 																	.".=>." ??? -->
	</xsl:function>
	<xsl:function as="xs:string?" name="functx:replace-multi">
		<xsl:param as="xs:string?" name="arg"></xsl:param>
		<xsl:param as="xs:string*" name="changeFrom"></xsl:param>
		<xsl:param as="xs:string*" name="changeTo"></xsl:param>
		<xsl:sequence select="if (count($changeFrom)>0) then functx:replace-multi(replace($arg,$changeFrom[1],functx:if-absent($changeTo[1],'')),$changeFrom[position()>1],$changeTo[position()>1]) else $arg"></xsl:sequence>
	</xsl:function>
	<xsl:function as="item()*" name="functx:if-absent">
		<xsl:param as="item()*" name="arg"></xsl:param>
		<xsl:param as="item()*" name="value"></xsl:param>
		<xsl:sequence select="if (exists($arg)) then $arg else $value"></xsl:sequence>
	</xsl:function>
	<!-- JUS: Get reverse date if the input is not an empty sting. 
	The input YYYY-MM-TT is converted to TT.MM.YYYY. -->
	<xsl:function name="func:get_reverse_date">
		<xsl:param name="input_date"></xsl:param>
		<xsl:if test="$input_date[.!='']">
			<xsl:value-of select="concat(substring($input_date,9,2),'.',substring($input_date,6,2),'.',substring($input_date,1,4))"></xsl:value-of>
		</xsl:if>
	</xsl:function>
	<xsl:function name="func:getCitationStyleForJournal">
		<xsl:param name="idType"></xsl:param>
		<xsl:param name="idValue"></xsl:param>
		<xsl:value-of select="jfunc:getCitationStyleForJournal($idType,$idValue)"></xsl:value-of>
	</xsl:function>
	<!--6 ed, 179 instl. 
	first substrings after the comma -> '179 instl.', 
	next before the instl -> '179'
	gives the number of instalment back -->
	<xsl:function name="func:substringAfterEdition">
		<xsl:param name="inputWithSpaceComma"></xsl:param>
		<xsl:value-of select="substring-before(substring-after($inputWithSpaceComma,','),' ')"></xsl:value-of>
	</xsl:function>
	<!--6 ed, 179 instl. 
	first substrings before the comma -> '6 ed', 
	next before the ed -> '6'
	gives the number of edition back -->
	<xsl:function name="func:substringBeforeEdition">
		<xsl:param name="inputWithSpaceComma"></xsl:param>
		<xsl:value-of select="substring-before(substring-before($inputWithSpaceComma,', '),' ')"></xsl:value-of>
	</xsl:function>
	<!--179 instl. 
	substrings befrore instalment
	gives the number of instalment back -->
	<xsl:function name="func:substringBeforeInstalment">
		<xsl:param name="inputWithInstalment"></xsl:param>
		<xsl:value-of select="if (contains($inputWithInstalment,'instl')) then substring-before($inputWithInstalment,'instl') else if (contains($inputWithInstalment,'Lf')) then substring-before($inputWithInstalment,'Lf') else ''"></xsl:value-of>
	</xsl:function>
	<xsl:function name="func:substringBeforeSince">
		<xsl:param name="inputWithSince"></xsl:param>
		<xsl:value-of select=" if (contains($inputWithSince,'since')) then substring-before($inputWithSince,'since') else if (contains($inputWithSince,'seit')) then substring-before($inputWithSince,'seit') else ''"></xsl:value-of>
	</xsl:function>
	<xsl:function name="func:substringAfterSince">
		<xsl:param name="inputWithSince"></xsl:param>
		<xsl:value-of select="if (contains($inputWithSince,'since')) then substring-after($inputWithSince,'since') else if (contains($inputWithSince,'seit')) then substring-after($inputWithSince,'seit') else ''"></xsl:value-of>
	</xsl:function>
	<xsl:function name="func:substringAfterReviewOf">
		<xsl:param name="inputReviewTitle"></xsl:param>
		<xsl:value-of select="substring-after($inputReviewTitle,'Review of:')"></xsl:value-of>
	</xsl:function>
</functions>
